---
subcategory: "Cloud Billing"
description: |-
 Collection of resources to manage IAM policy for a Billing Account.
---

# IAM policy for Billing Account

Three different resources help you manage IAM policies on billing accounts. Each of these resources serves a different use case:

* `google_billing_account_iam_policy`: Authoritative. Sets the IAM policy for the billing accounts and replaces any existing policy already attached.
* `google_billing_account_iam_binding`: Authoritative for a given role. Updates the IAM policy to grant a role to a list of members. Other roles within the IAM policy for the table are preserved.
* `google_billing_account_iam_member`: Non-authoritative. Updates the IAM policy to grant a role to a new member. Other members for the role of the billing accounts are preserved.

~> **Note:** `google_billing_account_iam_policy` **cannot** be used in conjunction with `google_billing_account_iam_binding` and `google_billing_account_iam_member` or they will fight over what your policy should be. In addition, be careful not to accidentally unset ownership of the billing account as `google_billing_account_iam_policy` replaces the entire policy.

~> **Note:** `google_billing_account_iam_binding` resources **can be** used in conjunction with `google_billing_account_iam_member` resources **only if** they do not grant privilege to the same role.

## google\_billing\_account\_iam\_policy

```hcl
data "google_iam_policy" "admin" {
  binding {
    role = "roles/billing.viewer"
    members = [
      "user:jane@example.com",
    ]
  }
}

resource "google_billing_account_iam_policy" "editor" {
  billing_account_id = "00AA00-000AAA-00AA0A"
  policy_data        = data.google_iam_policy.admin.policy_data
}
```

## google\_billing\_account\_iam\_binding

```hcl
resource "google_billing_account_iam_binding" "editor" {
  billing_account_id = "00AA00-000AAA-00AA0A"
  role               = "roles/billing.viewer"
  members = [
    "user:jane@example.com",
  ]
}
```

## google\_billing\_account\_iam\_member

```hcl
resource "google_billing_account_iam_member" "editor" {
  billing_account_id = "00AA00-000AAA-00AA0A"
  role               = "roles/billing.viewer"
  member             = "user:jane@example.com"
}
```

## Argument Reference

The following arguments are supported:

* `billing_account_id` - (Required) The billing account id.

For `google_billing_account_iam_member` or `google_billing_account_iam_binding`:

* `member/members` - (Required) Identities that will be granted the privilege in `role`.
  Each entry can have one of the following values:
  * **user:{emailid}**: An email address that represents a specific Google account. For example, alice@gmail.com or joe@example.com.
  * **serviceAccount:{emailid}**: An email address that represents a service account. For example, my-other-app@appspot.gserviceaccount.com.
  * **group:{emailid}**: An email address that represents a Google group. For example, admins@example.com.
  * **domain:{domain}**: A G Suite domain (primary, instead of alias) name that represents all the users of that domain. For example, google.com or example.com.

* `role` - (Required) The role that should be applied. Only one
    `google_billing_account_iam_binding` can be used per role. Note that custom roles must be of the format
    `[projects|organizations]/{parent-name}/roles/{role-name}`. Read more about roles [here](https://cloud.google.com/bigtable/docs/access-control#roles).

`google_billing_account_iam_policy` only:
* `policy_data` - (Required) The policy data generated by a `google_iam_policy` data source.

- - -

## Attributes Reference

In addition to the arguments listed above, the following computed attributes are
exported:

* `etag` - (Computed) The etag of the billing account's IAM policy.

## Import


-> **Custom Roles**: If you're importing a IAM resource with a custom role, make sure to use the
 full name of the custom role, e.g. `organizations/my-org-id/roles/my-custom-role`.

### Importing IAM members

IAM member imports use space-delimited identifiers that contain the resource's `billing_account_id`, `role`, and `member`. For example:

* `"{{billing_account_id}} roles/billing.user user:jane@example.com"`

An [`import` block](https://developer.hashicorp.com/terraform/language/import) (Terraform v1.5.0 and later) can be used to import IAM members:

```tf
import {
  id = "{{billing_account_id}} roles/billing.user user:jane@example.com"
  to = google_billing_account_iam_member.default
}
```

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can also be used:

```
$ terraform import google_billing_account_iam_member.default "{{billing_account_id}} roles/billing.user user:jane@example.com"
```

### Importing IAM bindings

IAM binding imports use space-delimited identifiers that contain the resource's `billing_account_id` and `role`. For example:

* `"{{billing_account_id}} roles/billing.user"`

An [`import` block](https://developer.hashicorp.com/terraform/language/import) (Terraform v1.5.0 and later) can be used to import IAM bindings:

```tf
import {
  id = "{{billing_account_id}} roles/billing.user"
  to = google_billing_account_iam_binding.default
}
```

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can also be used:

```
$ terraform import google_billing_account_iam_binding.default "{{billing_account_id}} roles/billing.user"
```

### Importing IAM policies

IAM policy imports use the `billing_account_id` identifier of the Billing Account resource only. For example:

* `{{billing_account_id}}`

An [`import` block](https://developer.hashicorp.com/terraform/language/import) (Terraform v1.5.0 and later) can be used to import IAM policies:

```tf
import {
  id = {{billing_account_id}}
  to = google_billing_account_iam_policy.default
}
```

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can also be used:

```
$ terraform import google_billing_account_iam_policy.default {{billing_account_id}}
```
